#!/usr/bin/perl -w
# modula3_count - count physical lines of code
# Strips out (* .. *) and counts the rest.
# Usage: modula3_count [-f file] [list_of_files]
#  file: file with a list of files to count (if "-", read list from stdin)
#  list_of_files: list of files to count
#  -f file or list_of_files can be used, or both

$total_sloc = 0;

# Do we have "-f" (read list of files from second argument)?
if (($#ARGV >= 1) && ($ARGV[0] eq "-f")) {
  # Yes, we have -f
  if ($ARGV[1] eq "-") {
    # The list of files is in STDIN
    while (<STDIN>) {
      chomp ($_);
      &count_file ($_);
    }
  } else {
    # The list of files is in the file $ARGV[1]
    open (FILEWITHLIST, $ARGV[1]) || die "Error: Could not open $ARGV[1]\n";
    while (<FILEWITHLIST>) {
      chomp ($_);
      &count_file ($_);
    }
    close FILEWITHLIST;
  }
  shift @ARGV; shift @ARGV;
}
# Process all (remaining) arguments as file names
while ($file = shift @ARGV) {
  &count_file ($file);
}

print "Total:\n";
print "$total_sloc\n";

sub count_file {
  my ($file) = @_;
  my $sloc = 0;
  my $incomment = 0;

  open (FILE, $file);
  while (<FILE>) {
    if ($incomment) {
      if (m/\*\)/) { s/^.*?\*\)//;  $incomment = 0;}
      else { s/.*//; }
    }
    if (!$incomment) {
      s!\(\*.*?\*\)!!g;
      if (m/\(\*/) {
        s/\(\*.*//;
        $incomment = 1;
      }
    }
    if (m/\S/) {$sloc++;}
  }
  print "$sloc $file\n";
  if ($incomment) {print "ERROR: ended in comment in $file\n";}
  $total_sloc += $sloc;
  $sloc = 0;
  $incomment = 0;
  close (FILE);
}
